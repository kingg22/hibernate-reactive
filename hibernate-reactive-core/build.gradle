import org.jetbrains.kotlin.gradle.dsl.JvmDefaultMode
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    id "hr-java-library"
    id "hr-test-containers"
    id "hr-print-resolved-version"
	alias(libs.plugins.kotlin.jvm)
	alias(libs.plugins.kotlin.all.open)
}

ext {
    mavenPomName = 'Hibernate Reactive Core'
}

description = 'The core module of Hibernate Reactive'

apply from: publishScript

dependencies {

    api(libs.org.hibernate.orm.hibernate.core)
	compileOnly(libs.org.hibernate.models)

    api(libs.io.smallrye.reactive.mutiny)
	// Coroutines
	api(libs.kotlinx.coroutines)

    //Logging
    implementation(libs.org.jboss.logging.jboss.logging)
    annotationProcessor(libs.org.jboss.logging.jboss.logging)

    compileOnly(libs.org.jboss.logging.jboss.logging.annotations)
    annotationProcessor(libs.org.jboss.logging.jboss.logging.annotations)
    annotationProcessor(libs.org.jboss.logging.jboss.logging.processor)


    //Specific implementation details of Hibernate Reactive:
    implementation(libs.io.vertx.vertx.sql.client)

    // Testing
    testImplementation(libs.org.assertj.assertj.core)
    testImplementation(libs.io.vertx.vertx.junit5)

	testImplementation(libs.kotlinx.coroutines.test)

    // Drivers
    testImplementation(libs.io.vertx.vertx.pg.client)
    testImplementation(libs.io.vertx.vertx.mysql.client)
    testImplementation(libs.io.vertx.vertx.db2.client)
    testImplementation(libs.io.vertx.vertx.mssql.client)
    testImplementation(libs.io.vertx.vertx.oracle.client)

	// Some tests using JSON need a formatter
	testRuntimeOnly(libs.com.fasterxml.jackson.core.jackson.databind)

    // Metrics
    testImplementation(libs.io.vertx.vertx.micrometer.metrics)

    // Optional dependency of vertx-pg-client, essential when connecting via SASL SCRAM
    testImplementation(libs.com.ongres.scram.scram.client)

    // JUnit Jupiter
    testImplementation(libs.org.junit.jupiter.junit.jupiter.api)
    testRuntimeOnly(libs.org.junit.jupiter.junit.jupiter.engine)
    testRuntimeOnly(libs.org.junit.platform.junit.platform.launcher)

    // JDBC driver to test with ORM and PostgreSQL
    testRuntimeOnly(libs.org.postgresql.postgresql)

    // JDBC driver for Testcontainers with MS SQL Server
    testRuntimeOnly(libs.com.microsoft.sqlserver.mssql.jdbc)

    // JDBC driver for Testcontainers with MariaDB Server
    testRuntimeOnly(libs.org.mariadb.jdbc.mariadb.java.client)

    // JDBC driver for Testcontainers with MYSQL Server
    testRuntimeOnly(libs.com.mysql.mysql.connector.j)

    // JDBC driver for Db2 server, for testing
    testRuntimeOnly(libs.com.ibm.db2.jcc)

    // EHCache
    testRuntimeOnly(libs.org.ehcache.ehcache) {
            capabilities {
                requireCapability 'org.ehcache.modules:ehcache-xml-jakarta'
            }
    }
    testRuntimeOnly(libs.org.hibernate.orm.hibernate.jcache)

    // log4j
    testRuntimeOnly(libs.org.apache.logging.log4j.log4j.core)

    // Testcontainers
    testImplementation(libs.org.testcontainers.postgresql)
    testImplementation(libs.org.testcontainers.mysql)
    testImplementation(libs.org.testcontainers.mariadb)
    testImplementation(libs.org.testcontainers.db2)
    testImplementation(libs.org.testcontainers.cockroachdb)
    testImplementation(libs.org.testcontainers.mssqlserver)
    testImplementation(libs.org.testcontainers.oracle.xe)
}

// Reproducible Builds

// https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
// Configure archive tasks to produce reproducible archives:
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

kotlin {
	compilerOptions {
		apiVersion.set( KotlinVersion.KOTLIN_2_0)
		languageVersion.set(apiVersion)
		optIn.add("kotlin.contracts.ExperimentalContracts")
		freeCompilerArgs.add("-XXexplicit-return-types=strict")
		jvmDefault.set( JvmDefaultMode.NO_COMPATIBILITY)
		extraWarnings.set(true)
		allWarningsAsErrors.set(true)
	}
}

allOpen {
	annotation("org.hibernate.reactive.coroutines.HibernateReactiveOpen")
}

// Import version from settings.gradle in root, all are defined as JavaLanguageVersion
if (gradle.ext.javaToolchainEnabled) {
	// Need to align kotlin compiler to java compiler version for main and test source sets
	tasks.named( "compileKotlin", KotlinCompilationTask) {
		compilerOptions {
			jvmTarget.set( JvmTarget.fromTarget( gradle.ext.javaVersions.main.release.toString()))
		}
	}
	tasks.named("compileTestKotlin", KotlinCompilationTask) {
		compilerOptions {
			jvmTarget.set(JvmTarget.fromTarget(gradle.ext.javaVersions.test.release.toString()))
		}
	}
} else {
	kotlin {
		compilerOptions {
			// Only set global compiler options because in root have the same version for main and test
			jvmTarget.set(JvmTarget.fromTarget(gradle.ext.javaVersions.main.release.toString()))
		}
	}
}
